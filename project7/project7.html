<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>
	Gregory Nothnagel, CS 535, Project #7, 
	(See .js file for usage directions)
	</title>
    <style>
        body, html {
            margin: 0;
            padding: 0;
            overflow: hidden;
            width: 100%;
            height: 100%;
        }
        canvas {
            display: block;
            width: 100%;
            height: 100%;
        }
    </style>
</head>
<body>
    <canvas id="glcanvas"></canvas>

    <script type="x-shader/x-vertex" id="vertex-shader"> #version 300 es
        in vec4 aPosition;
        void main() {
            gl_Position = aPosition;
        }
    </script>

    <script type="x-shader/x-fragment" id="fragment-shader"> #version 300 es
	
        precision mediump float;
		
		// If true, block colors fade nicely into other blocks, otherwise each block gets a flat color
		// (all block colors and positions determined procedurally)
		bool SMOOTHING = true;
        
        uniform vec2 iResolution;
		uniform vec3 eyePos;
		uniform vec3 viewRotation;
		
		const int numWaves = 10;
		layout(std140) uniform WaveGroup { // 8 float32's per wave = 32 bytes
			vec4 slopes[numWaves]; // 4 floats 32's = 16 bytes
			vec2 FA[numWaves]; // also 4 float 32's (due to padding) = 16 bytes
			// "FA" means frequency and amplitude. Those are the values the vec2 stores
		};

		vec3 findVoxel(vec3 curVoxelPos) { // if voxel is in WaveGroup, returns its color, else returns -1
			
			bool flag = false;
			
			float sum = 0.0;
			
			for (int wave = 0; wave < numWaves; wave++){
				sum += FA[wave].y * sin(FA[wave].x * (slopes[wave].x * curVoxelPos.x + slopes[wave].y * curVoxelPos.y + slopes[wave].z * curVoxelPos.z + slopes[wave].w));
				// sum += [Amplitude] * sin ([Frequency] * [Direction])
			}
			
			if (curVoxelPos.y < 0.0){
				// if player height below zero, simple threshold
				// if sum of sine waves > 0, block is in that location, otherwise no block
				if (sum > 0.0) flag = true;
				else flag = false;
			}
			else {
				// if player height above zero, modified threshold
				// if sum of sine waves > player height, block is in that location, otherwise no block
				// this way, terrain fades away towards the sky and there is an apparent "sky" region and an apparent "underground" region.
				// Sky stretches infinitely up, ground terrain stretches infinitely down
				if (sum > curVoxelPos.y) flag = true;
				else flag = false;
			}
			
			// procedural color using modulo operator
			if (flag) return vec3(mod(curVoxelPos.x, 2.0) / 2.0, mod(curVoxelPos.y, 2.0) / 2.0, mod(curVoxelPos.z, 2.0) / 2.0);
			else return vec3(-1.0, 0.0, 0.0);
			
		}
		
		// simulates raymarching: returns location of next voxel given current ray voxel location and view direction
		// Does not return or accept EXACT intersection location of view with voxel.
		// Just accepts location of center of current voxel, returns location of center of next voxel
		vec3 nextVoxel(vec3 curVoxelPos, vec3 viewDir){
		
			vec3 step = sign(viewDir);
		
			vec3 nextVoxelPos1 = curVoxelPos;
			vec3 nextVoxelPos2 = curVoxelPos;
			vec3 nextVoxelPos3 = curVoxelPos;
			
			nextVoxelPos1.x += step.x;
			nextVoxelPos2.y += step.y;
			nextVoxelPos3.z += step.z;
			
			
			vec3 wall = curVoxelPos + step * 0.5;
			
			vec3 x_int = eyePos + viewDir * ((wall.x - eyePos.x) / viewDir.x);
			vec3 y_int = eyePos + viewDir * ((wall.y - eyePos.y) / viewDir.y);
			vec3 z_int = eyePos + viewDir * ((wall.z - eyePos.z) / viewDir.z);
			
			float d1 = max(abs(x_int.y - nextVoxelPos1.y), abs(x_int.z - nextVoxelPos1.z));
			float d2 = max(abs(y_int.x - nextVoxelPos2.x), abs(y_int.z - nextVoxelPos2.z));
			float d3 = max(abs(z_int.x - nextVoxelPos3.x), abs(z_int.y - nextVoxelPos3.y));
			
			if (d1 <= d2 && d1 <= d3) return nextVoxelPos1;
			if (d2 <= d1 && d2 <= d3) return nextVoxelPos2;
			if (d3 <= d1 && d3 <= d2) return nextVoxelPos3;
			
		}
		
		// given voxel position and view direction, where does the view direction intersect with this voxel EXACTLY?
		vec3 getIntersection(vec3 curVoxelPos, vec3 viewDir){
		
			vec3 wall = curVoxelPos - sign(viewDir) * .5;
			
			vec3 x_int = eyePos + viewDir * ((wall.x - eyePos.x) / viewDir.x);
			vec3 y_int = eyePos + viewDir * ((wall.y - eyePos.y) / viewDir.y);
			vec3 z_int = eyePos + viewDir * ((wall.z - eyePos.z) / viewDir.z);
			
			float dx = length(curVoxelPos - x_int);
			float dy = length(curVoxelPos - y_int);
			float dz = length(curVoxelPos - z_int);
			
			if (dx <= dy && dx <= dz) return x_int;
			if (dy <= dx && dy <= dz) return y_int;
			if (dz <= dx && dz <= dy) return z_int;
			
		}

		// changes view direction before raymarching begins
		vec3 applyRotation(vec3 viewDir){
		
			// Compute the sines and cosines of theta for each of the three axes
			vec3 angles = radians(viewRotation);
			vec3 c = cos(angles);
			vec3 s = sin(angles);

			// Rotation matrices for x, y, z axes
			mat3 rx = mat3(1.0, 0.0, 0.0,
						   0.0, c.x, s.x,
						   0.0, -s.x, c.x);
			mat3 ry = mat3(c.y, 0.0, -s.y,
						   0.0, 1.0, 0.0,
						   s.y, 0.0, c.y);
			mat3 rz = mat3(c.z, s.z, 0.0,
						   -s.z, c.z, 0.0,
						   0.0, 0.0, 1.0);
						   
			return ry * rx * rz * viewDir; // order matters here
		}

		out vec4 fragColor;
	
        void main() {
            vec2 fragCoord = gl_FragCoord.xy / iResolution.y;
            vec2 uv = vec2(fragCoord.x * 2.0 - (iResolution.x / iResolution.y), fragCoord.y * 2.0 - 1.0);
            
            vec3 viewDir = normalize(vec3(uv, 1.0)); // ray direction
			viewDir = applyRotation(viewDir);
            vec3 finalColor = vec3(0.0, 0.0, 0.0); // final pixel color
            
			// initial voxel position starts at the player's eye position, rounded
			vec3 curVoxelPos = vec3(floor(eyePos.x + 0.5), floor(eyePos.y + 0.5), floor(eyePos.z + 0.5));
			
			int RENDER_DISTANCE = 500;
            for (int i = 0; i < RENDER_DISTANCE; i++) {
				curVoxelPos = nextVoxel(curVoxelPos, viewDir);
				vec3 color = findVoxel(curVoxelPos);
				if (color.x >= 0.0){
				
					if (SMOOTHING){
						//get intersection position and eight surrounding voxels
						vec3 ip = getIntersection(curVoxelPos, viewDir); // "ip" for intersection position
						
						finalColor = (sin(ip * .5) + vec3(1.0, 1.0, 1.0)) / 2.0;
					}
					else finalColor = (sin(curVoxelPos * .5) + vec3(1.0, 1.0, 1.0)) / 2.0;
					
					break;
				}
            }
            
			// dot cursor, just a basic overlay, no functionality
			if (length(fragCoord - vec2(.5 * (iResolution.x / iResolution.y), .5)) < .01) finalColor = finalColor * .5 + vec3(1.0, 1.0, 1.0) * .5;
			
			fragColor = vec4(finalColor, 1.0);
        }
    </script>

	<script src="project7.js"></script>

</body>
</html>
